plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

archivesBaseName = archivesBaseName + "-js-extension"

sourceCompatibility = JavaVersion.values()[project.java_version.toInteger() - 1]
targetCompatibility = JavaVersion.values()[project.java_version.toInteger() - 1]

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(project.java_version))
    }
}

configurations {
    jsmacrosExtensionInclude
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.guava:guava:32.0.1-jre"
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "org.slf4j:slf4j-api:1.7.32"
    implementation "it.unimi.dsi:fastutil:8.5.6"

    implementation "org.graalvm.sdk:graal-sdk:${project.graal_version}"
    implementation "org.graalvm.js:js:${project.graal_version}"
    implementation "org.graalvm.truffle:truffle-api:${project.graal_version}"
    implementation "org.graalvm.regex:regex:${project.graal_version}"
//		implementation "org.graalvm.compiler:compiler:${project.graal_version}"
//		implementation "org.graalvm.compiler:compiler-management:${project.graal_version}"

    jsmacrosExtensionInclude "org.graalvm.sdk:graal-sdk:${project.graal_version}"
    jsmacrosExtensionInclude "org.graalvm.js:js:${project.graal_version}"
    jsmacrosExtensionInclude "org.graalvm.truffle:truffle-api:${project.graal_version}"
    jsmacrosExtensionInclude "org.graalvm.regex:regex:${project.graal_version}"
//		jsmacrosExtensionInclude "org.graalvm.compiler:compiler:${project.graal_version}"
//		jsmacrosExtensionInclude "org.graalvm.compiler:compiler-management:${project.graal_version}"

    implementation(rootProject.sourceSets.core.output)

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.jetbrains:annotations:20.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

}

processResources {
    filesMatching("jsmacros.ext.graal.json") {
        expand "dependencies": configurations.jsmacrosExtensionInclude.files.collect { it.name }.join(" ")
    }
}

jar {
    from(configurations.jsmacrosExtensionInclude.files) {
        include '*'
        into "META-INF/jsmacrosdeps"
    }

    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

test {
    useJUnitPlatform()
}