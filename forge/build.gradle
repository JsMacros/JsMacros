plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

archivesBaseName = archivesBaseName + "-FORGE"

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

minecraft {
    forge {
        mixinConfig = ["jsmacros-forge.mixins.json", "jsmacros.mixins.json"]
        devNamespace = "yarn"
        devFallbackNamespace = "intermediary"
    }

    mcRemapper.tinyRemapperConf = {
        ignoreFieldDesc(true)
        ignoreConflicts(true)
    }

//    mcRemapper.afterRemap = {
//        ClassReader reader = new ClassReader(Files.newInputStream(getPath("/net/minecraft/entity/mob/MobEntity.class")))
//        CheckClassAdapter.verify(reader, false, new PrintWriter(System.out))
//        System.out.println("Verified!")
//    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    localRuntime "org.graalvm.sdk:graal-sdk:${project.graal_version}"
    localRuntime "org.graalvm.js:js:${project.graal_version}"
    localRuntime "org.graalvm.truffle:truffle-api:${project.graal_version}"
    localRuntime "org.graalvm.regex:regex:${project.graal_version}"
//		localRuntime "org.graalvm.compiler:compiler:${project.graal_version}"
//		localRuntime "org.graalvm.compiler:compiler-management:${project.graal_version}"

    shadowCommon "io.noties:prism4j:${project.prism_version}", {
        exclude module: "annotations-java5"
    }
    shadowCommon "com.neovisionaries:nv-websocket-client:2.10"
    shadowCommon "org.javassist:javassist:3.28.0-GA"
    shadowCommon "org.jooq:joor:0.9.14"

    for (sourceSet in project(":common").sourceSets) {
        if (sourceSet == project(":common").sourceSets.test) continue
        common sourceSet.output
        shadowCommon sourceSet.output
    }

    implementation project(":javascript_extension").sourceSets.main.output
    jsmacrosExtensionInclude(project(":javascript_extension")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }

    filesMatching("jsmacros.extension.json") {
        expand "dependencies": configurations.jsmacrosExtensionInclude.files.collect { it.name }.join(" ")
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"

    // exclude icu that's "accidentally" (I hope) included in graaljs jar
    exclude "com/ibm/**"

    mergeServiceFiles()
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null

    from(configurations.jsmacrosExtensionInclude.files) {
        include '*'
        into "META-INF/jsmacrosdeps"
    }
}

jar {
    classifier "dev"

    manifest {
        attributes([
                'MixinConnector': 'xyz.wagyourtail.jsmacros.forge.client.JsMacrosEarlyRiser',
        ])
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

task createDist(type: Copy) {
    from(new File(project.buildDir, "libs"))
    into new File(rootProject.rootDir, "dist")
}

build.finalizedBy(createDist)
