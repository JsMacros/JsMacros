plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

archivesBaseName = archivesBaseName + "-FORGE"

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    include
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

loom {
    forge {
        mixinConfigs "jsmacros-forge.mixins.json"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"


    localRuntime "org.graalvm.sdk:graal-sdk:${project.graal_version}"
    localRuntime "org.graalvm.js:js:${project.graal_version}"
    localRuntime "org.graalvm.truffle:truffle-api:${project.graal_version}"
    localRuntime "org.graalvm.regex:regex:${project.graal_version}"
//		localRuntime "org.graalvm.compiler:compiler:${project.graal_version}"
//		localRuntime "org.graalvm.compiler:compiler-management:${project.graal_version}"

    include "io.noties:prism4j:${project.prism_version}", {
        exclude module: "annotations-java5"
    }
    include 'com.ibm.icu:icu4j:70.1'
    include "com.neovisionaries:nv-websocket-client:2.10"
    include "org.javassist:javassist:3.28.0-GA"
    include "com.mojang:brigadier:${project.brigadier_version}"


    implementation 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    shadowCommon "org.spongepowered:mixin:0.7.11-SNAPSHOT"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }

    //ij wasn't seeing this, even tho it's part of namedElements...
    implementation(project(":common").sourceSets.core.output)
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }

    common(project(path: ":javascript_extension", configuration: "namedElements")) { transitive false }
    jsmacrosExtensionInclude(project(path: ":javascript_extension", configuration: "namedElements")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
        expand "mcversion": project.minecraft_version
    }

    filesMatching("jsmacros.extension.json") {
        expand "dependencies": configurations.jsmacrosExtensionInclude.files.collect { it.name }.join(" ")
    }

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"

    // exclude icu that's "accidentally" (I hope) included in graaljs jar
    exclude "com/ibm/**"
    exclude "META-INF/**.SF"
    exclude "META-INF/**.RSA"
    exclude "META-INF/**.DSA"

    mergeServiceFiles()
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null

    from (configurations.jsmacrosExtensionInclude.files) {
        include '*'
        into "META-INF/jsmacrosdeps"
    }

    from(configurations.include.files) {
        include '*'
        into 'META-INF/jars/'
    }
}

jar {
    classifier "dev"

    manifest {
        attributes([
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'FMLCorePlugin': 'xyz.wagyourtail.jsmacros.forge.client.JsMacrosEarlyRiser',
                'JsMacrosDeps': configurations.include.files.collect { it.name }.join(" "),
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true
        ])
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

task createDist(type: Copy) {
    from(new File(project.buildDir, "libs"))
    into new File(rootProject.rootDir, "dist")
}

build.finalizedBy(createDist)