import org.gradle.internal.jvm.Jvm
import xyz.wagyourtail.tiny2mcp.Tiny2MCP

buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.github.wagyourtail:forgegradle:7747de8fe7'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.6-SNAPSHOT'
        classpath files(Jvm.current().toolsJar)
    }
}

subprojects {
    apply plugin: 'net.minecraftforge.gradle.forge'
    apply plugin: 'org.spongepowered.mixin'

    repositories {
        flatDir {
            dirs "$rootProject.rootDir/libs"
        }
    }

    configurations {
        coreImplementation {
            extendsFrom implementation
        }
        jsmacrosExtensionInclude
    }

    minecraft {
        version = "1.8.9-11.15.1.1722"
        runDir = "run"

        mappings = Tiny2MCP.remap(new File(rootProject.rootDir, "libs"), "1.8.9", "build.202110221052")
    }

    configurations {
        include
        include.transitive = false
    }
    sourceSets {
        main {
            compileClasspath += configurations.coreImplementation
            runtimeClasspath += configurations.coreImplementation
        }
    }
    dependencies {
        implementation 'org.spongepowered:mixin:0.7.11-SNAPSHOT'

        // non-mod dependencies:
        implementation 'com.ibm.icu:icu4j:70.1'
        implementation "io.noties:prism4j:${project.prism_version}"

        coreImplementation "com.neovisionaries:nv-websocket-client:2.10"
        coreImplementation "org.javassist:javassist:3.28.0-GA"
        implementation "com.mojang:brigadier:${project.brigadier_version}"

        implementation group: 'org.jetbrains', name: 'annotations', version: '16.0.1'
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name "Modmuss50 Repository"
            url 'https://maven.fabricmc.net/'
        }
        maven {
            name = "legacy-fabric"
            url = "https://maven.legacyfabric.net"
        }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.spongepowered.org/maven/' }
        mavenCentral()

    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        def version = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = version
        }
    }
    jar {

        def files = []
        def jsons = []

        eachFile {
            files.add(it)
        }

        filesMatching("**/*.mixins.json") {
            def json = new groovy.json.JsonSlurper().parse(it.open())
            jsons.add(json)
        }

        doLast {
            def pkgs = new ArrayList()
            def client = new ArrayList()
            def server = new ArrayList()
            def mixins = new ArrayList()
            jsons.forEach({
                def pkg = it.package
                pkgs.add(pkg.replace(".", "/"))
                def cl = it.client
                if (cl != null) {
                    cl.forEach({
                        client.add((pkg + "." + it).replace(".", "/"))
                    })
                }
                def sl = it.server
                if (sl != null) {
                    sl.forEach({
                        server.add((pkg + "." + it).replace(".", "/"))
                    })
                }
                def ml = it.mixins
                if (ml != null) {
                    ml.forEach({
                        mixins.add((pkg + "." + it).replace(".", "/"))
                    })
                }
            })
            files.forEach({
                def name = it.relativePath.toString()
                for (def cl in client) {
                    if (name.endsWith(cl + ".class")) {
                        return
                    }
                }
                for (def sl in server) {
                    if (name.endsWith(sl + ".class")) {
                        return
                    }
                }
                for (def ml in mixins) {
                    if (name.endsWith(ml + ".class")) {
                        return
                    }
                }
                for (def pkg in pkgs) {
                    if (name.contains(pkg + "/") && name.endsWith(".class")) {
                        System.err.println("File " + name + " is not in a mixin json")
                    }
                }
            })
        }
    }
}

task removeDist(type: Delete) {
    delete new File(rootProject.rootDir, "dist")
}

clean.finalizedBy(removeDist)
