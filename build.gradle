plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    configurations {
        coreImplementation {
            extendsFrom implementation
        }
        jsmacrosExtensionInclude
    }

    sourceSets {
        main {
            compileClasspath += configurations.coreImplementation
            runtimeClasspath += configurations.coreImplementation
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

        // non-mod dependencies:
        implementation "io.noties:prism4j:${project.prism_version}"

        coreImplementation "com.neovisionaries:nv-websocket-client:2.10"
        coreImplementation "org.javassist:javassist:3.28.0-GA"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name "Modmuss50 Repository"
            url 'https://maven.fabricmc.net/'
            metadataSources {
                artifact()
            }
        }
        maven { url 'https://jitpack.io' }
        maven {
            url "https://maven.terraformersmc.com/releases"
            name "modmenu maven"
        }
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }

    jar {

        def files = []
        def jsons = []

        eachFile {
            files.add(it)
        }

        filesMatching("**/*.mixins.json") {
            def json = new groovy.json.JsonSlurper().parse(it.open())
            jsons.add(json)
        }

        doLast {
            def pkgs = new ArrayList()
            def client = new ArrayList()
            def server = new ArrayList()
            def mixins = new ArrayList()
            jsons.forEach({
                def pkg = it.package
                pkgs.add(pkg.replace(".", "/"))
                def cl = it.client
                if (cl != null) {
                    cl.forEach({
                        client.add((pkg + "." + it).replace(".", "/"))
                    })
                }
                def sl = it.server
                if (sl != null) {
                    sl.forEach({
                        server.add((pkg + "." + it).replace(".", "/"))
                    })
                }
                def ml = it.mixins
                if (ml != null) {
                    ml.forEach({
                        mixins.add((pkg + "." + it).replace(".", "/"))
                    })
                }
            })
            files.forEach({
                def name = it.relativePath.toString()
                for (def cl in client) {
                    if (name.endsWith(cl + ".class")) {
                        return
                    }
                }
                for (def sl in server) {
                    if (name.endsWith(sl + ".class")) {
                        return
                    }
                }
                for (def ml in mixins) {
                    if (name.endsWith(ml + ".class")) {
                        return
                    }
                }
                for (def pkg in pkgs) {
                    if (name.contains(pkg + "/") && name.endsWith(".class")) {
                        System.err.println("File " + name + " is not in a mixin json")
                    }
                }
            })
        }
    }
}

task removeDist(type: Delete) {
    delete new File(rootProject.rootDir, "dist")
}

clean.finalizedBy(removeDist)