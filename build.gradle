import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "xyz.wagyourtail.unimined"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

base {
    archivesName = project.archives_base_name
}

java {
    sourceCompatibility = JavaVersion.values()[project.java_version.toInteger() - 1]
    targetCompatibility = JavaVersion.values()[project.java_version.toInteger() - 1]
}

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name "fabricmc repository"
        url 'https://maven.fabricmc.net/'
    }
    maven {
        name "modmenu repository"
        url 'https://maven.terraformersmc.com/releases/'
    }
    maven {
        name "forge repository"
        url 'https://files.minecraftforge.net/maven/'
    }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

configurations {
    implementation {
        canBeResolved = true
    }
    jsmacrosExtensionInclude
    shadowForge
}

sourceSets {
    core {
        java {
            srcDir "common/src/core/java"
        }
        resources {
            srcDir "common/src/core/resources"
        }
        compileClasspath += configurations.implementation
        runtimeClasspath += configurations.implementation
    }
    client {
        compileClasspath += core.output + core.compileClasspath + main.output
        runtimeClasspath += core.output + core.runtimeClasspath + main.output
    }
    main {
        compileClasspath += core.output + core.compileClasspath
        runtimeClasspath += core.output + core.runtimeClasspath
    }
    if (enabled_loaders.contains("forge")) {
        forge {
            compileClasspath += core.output + core.compileClasspath + main.output + configurations.implementation + client.output
            runtimeClasspath += core.output + core.runtimeClasspath + main.output + configurations.implementation + client.output
        }
    }
    if (enabled_loaders.contains("fabric")) {
        fabric {
            compileClasspath += core.output + core.compileClasspath + main.output + configurations.implementation + client.output
            runtimeClasspath += core.output + core.runtimeClasspath + main.output + configurations.implementation + client.output
        }
    }
}

unimined.useGlobalCache = false

unimined.minecraft {
    version project.minecraft_version
    side("server")

    mappings {
        intermediary()
        yarn project.yarn_mappings

        stub.withMappings("official", ["intermediary", "yarn"]) {
            c("fzc", []) {
                m("b", "(Lzg;)V", ["method_14364", "sendPacket"])
            }
        }

        stub.withMappings("intermediary", ["yarn"]) {
            c("net/minecraft/class_1496", []) {
                m("method_56680", "()Lnet/minecraft/class_1263;", ["getInventoryVanilla"])
            }
            c("net/minecraft/class_329", []) {
                m("method_1759", "(Lnet/minecraft/class_332;Lnet/minecraft/class_9779;)V", ["renderHotbarVanilla"])
            }
        }

        devFallbackNamespace "intermediary"
    }

    accessWidener {
        accessWidener(file("src/main/resources/jsmacros.accesswidener"))
    }

    if (sourceSet == sourceSets.main || sourceSet == sourceSets.client) {
        defaultRemapJar = false
        runs.off = true
    }
}

unimined.minecraft(sourceSets.client) {
    combineWith(":core")
    combineWith(":main")
    side("combined")
}

sourceSets {
    core {
        compileClasspath += configurations.minecraftLibraries
        runtimeClasspath += configurations.minecraftLibraries
    }
}


if (enabled_loaders.contains("forge")) {

    unimined.minecraft(sourceSets.forge) {
        combineWith(sourceSets.main)
        side("combined")

        minecraftRemapper.config {
            ignoreConflicts(true)
        }

        neoForged {
            loader project.forge_version
            accessTransformer(aw2at(file("src/main/resources/jsmacros.accesswidener")))

            mixinConfig = [
                "jsmacros-forge.mixins.json",
                "jsmacros-client.mixins.json",
                "jsmacros-common.mixins.json",
            ]
        }
    }

}

if (enabled_loaders.contains("fabric")) {

    unimined.minecraft(sourceSets.fabric) {
        combineWith(sourceSets.main)
        side("combined")

        fabric {
            loader project.fabric_loader_version
            accessWidener(file("src/main/resources/jsmacros.accesswidener"))
        }
    }

}

configurations {
    clientCompileOnly.extendsFrom compileOnly
}

dependencies {
    implementation "io.noties:prism4j:${project.prism_version}"
    compileOnly "org.spongepowered:mixin:0.8.5"
    compileOnly "io.github.llamalad7:mixinextras-common:0.3.2"
    compileOnly "org.ow2.asm:asm:9.5"

    coreImplementation 'org.jooq:joor:0.9.15'
    coreImplementation "com.neovisionaries:nv-websocket-client:2.14"
    coreImplementation 'org.javassist:javassist:3.30.2-GA'

    jsmacrosExtensionInclude(project(":extension:graal")) { transitive = false }
    jsmacrosExtensionInclude(project(":extension:graal:js")) { transitive = false }

    if (enabled_loaders.contains("fabric")) {

        fabricModImplementation fabricApi.fabricModule("fabric-api-base", project.fapi_version)
        fabricModImplementation fabricApi.fabricModule("fabric-lifecycle-events-v1", project.fapi_version)
        fabricModImplementation fabricApi.fabricModule("fabric-key-binding-api-v1", project.fapi_version)
        fabricModImplementation fabricApi.fabricModule("fabric-resource-loader-v0", project.fapi_version)
        fabricModImplementation fabricApi.fabricModule("fabric-command-api-v2", project.fapi_version)

        fabricInclude fabricApi.fabricModule("fabric-api-base", project.fapi_version)
        fabricInclude fabricApi.fabricModule("fabric-lifecycle-events-v1", project.fapi_version)
        fabricInclude fabricApi.fabricModule("fabric-key-binding-api-v1", project.fapi_version)
        fabricInclude fabricApi.fabricModule("fabric-resource-loader-v0", project.fapi_version)
        fabricInclude fabricApi.fabricModule("fabric-command-api-v2", project.fapi_version)

        fabricInclude "io.noties:prism4j:${project.prism_version}"
        fabricInclude "com.neovisionaries:nv-websocket-client:2.14"
        fabricInclude "org.javassist:javassist:3.30.2-GA"
        fabricInclude "org.jooq:joor:0.9.14"

        fabricModImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
        fabricModImplementation fabricApi.fabricModule("fabric-screen-api-v1", project.fapi_version)

        fabricRuntimeOnly(project(":extension:graal"))
        fabricRuntimeOnly(project(":extension:graal:js"))
    }

    if (enabled_loaders.contains("forge")) {

        forgeInclude "io.noties:prism4j:${project.prism_version}", {
            exclude module: "annotations-java5"
        }
        forgeInclude "com.neovisionaries:nv-websocket-client:2.14"
        forgeInclude "org.javassist:javassist:3.30.2-GA"
        forgeInclude "org.jooq:joor:0.9.14"


        forgeRuntimeOnly(project(":extension:graal"))
        forgeRuntimeOnly(project(":extension:graal:js"))

    }
}

tasks.register('removeDist', Delete) {
    delete new File(rootProject.rootDir, "dist")
}

clean.finalizedBy(removeDist)

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    var javaVersion = project.java_version.toInteger()
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set javaVersion
    }
}

jar {
    enabled = false
}

if (enabled_loaders.contains("forge")) {
    processForgeResources {
        inputs.property "version", project.version

        filesMatching("META-INF/neoforge.mods.toml") {
            expand "version": project.version
        }

        filesMatching("jsmacros.extension.json") {
            expand "dependencies": configurations.jsmacrosExtensionInclude.files.collect { it.name }.join(" ")
        }
    }


    forgeJar {
        dependsOn(":extension:graal:jar")
        dependsOn(":extension:graal:js:jar")
        archiveClassifier = "forge-dev"
        from sourceSets.forge.output, sourceSets.main.output, sourceSets.core.output, sourceSets.client.output

        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    task shadowForgeJar(type: ShadowJar) {
        archiveClassifier = "forge-dev-shadow"
        from(forgeJar)

        // exclude icu that's "accidentally" (I hope) included in graaljs jar
        exclude "com/ibm/**"

        mergeServiceFiles()

        configurations = [project.configurations.shadowForge]
}

    remapForgeJar {
        dependsOn(shadowForgeJar)
        inputFile = shadowForgeJar.archiveFile.get().asFile

        from(configurations.jsmacrosExtensionInclude.files) {
            include '*'
            into "META-INF/jsmacrosdeps"
        }

        manifest {
            attributes([
                    'MixinConnector': 'xyz.wagyourtail.jsmacros.forge.client.JsMacrosEarlyRiser',
            ])
        }

        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}

if (enabled_loaders.contains("fabric")) {
    processFabricResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }

        filesMatching("jsmacros.extension.json") {
            expand "dependencies": configurations.jsmacrosExtensionInclude.files.collect { it.name }.join(" ")
        }
    }

    fabricJar {
        dependsOn(":extension:graal:jar")
        dependsOn(":extension:graal:js:jar")
        from sourceSets.fabric.output, sourceSets.main.output, sourceSets.core.output, sourceSets.client.output

        preserveFileTimestamps = false
        reproducibleFileOrder = true

        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        from(configurations.jsmacrosExtensionInclude.files) {
            include '*'
            into "META-INF/jsmacrosdeps"
        }
    }

    remapFabricJar {

        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}

task generatePyDoc(type: Javadoc) {
    group = "documentation"
    description = "Generates the python documentation for the project"
    source = sourceSets.main.allJava + sourceSets.core.allJava
    doFirst {
        classpath = sourceSets.main.compileClasspath + sourceSets.core.compileClasspath
    }
    destinationDir = new File(rootProject.buildDir, "docs/python/JsMacrosAC/")
    options.doclet = "xyz.wagyourtail.doclet.pydoclet.Main"
    options.docletpath new File(rootProject.rootDir, "buildSrc/build/libs/buildSrc.jar")
    options.addStringOption "v", version
}

task copyPyDoc(type: Copy, dependsOn: generatePyDoc) {
    group = "documentation"
    description = "Copies the python documentation to the build folder"
    from new File(rootProject.rootDir, "docs/python")
    into new File(rootProject.buildDir, "docs/python")
}

task generateTSDoc(type: Javadoc) {
    group = "documentation"
    description = "Generates the typescript documentation for the project"
    source = sourceSets.main.allJava + sourceSets.core.allJava
    doFirst {
        classpath = sourceSets.main.compileClasspath + sourceSets.core.compileClasspath
    }
    destinationDir = new File(rootProject.buildDir, "docs/typescript/headers/")
    options.doclet = "xyz.wagyourtail.doclet.tsdoclet.Main"
    options.docletpath new File(rootProject.rootDir, "buildSrc/build/libs/buildSrc.jar")
    options.addStringOption "v", version
}

task copyTSEnv(type: Copy, dependsOn: generateTSDoc) {
    group = "documentation"
    description = "Copies the typescript files to the build folder"
    from new File(rootProject.rootDir, "docs/typescript")
    into new File(rootProject.buildDir, "docs/typescript")
}

task generateWebDoc(type: Javadoc) {
    group = "documentation"
    description = "Generates the web documentation for the project"
    source = sourceSets.main.allJava + sourceSets.core.allJava
    doFirst {
        classpath = sourceSets.main.compileClasspath + sourceSets.core.compileClasspath
    }
    destinationDir = new File(rootProject.buildDir, "docs/web/")
    options.doclet = "xyz.wagyourtail.doclet.webdoclet.Main"
    options.docletpath new File(rootProject.rootDir, "buildSrc/build/libs/buildSrc.jar")
    options.addStringOption "v", version
    options.addStringOption "mcv", rootProject.minecraft_version
    options {
        links 'https://docs.oracle.com/javase/8/docs/api/', 'https://www.javadoc.io/doc/org.slf4j/slf4j-api/1.7.30/', 'https://javadoc.io/doc/com.neovisionaries/nv-websocket-client/latest/'
    }
}

task copyWebDoc(type: Copy, dependsOn: generateWebDoc) {
    group = "documentation"
    description = "Copies the web documentation to the build folder"
    from new File(rootProject.rootDir, "docs/web")
    into new File(rootProject.buildDir, "docs/web")

    inputs.property "version", project.version

    filesMatching("index.html") {
        expand "version": project.version
    }
}

task createDist(type: Copy) {
    group = "build"
    description = "Creates all files for the distribution of the project"
    dependsOn copyPyDoc, copyTSEnv, copyWebDoc
    from(new File(rootProject.buildDir, "docs"))
    from(new File(project.buildDir, "libs"))
    into new File(rootProject.rootDir, "dist")
}

build.finalizedBy(createDist)